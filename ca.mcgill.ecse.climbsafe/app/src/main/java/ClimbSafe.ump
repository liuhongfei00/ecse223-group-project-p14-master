namespace ca.mcgill.ecse.climbsafe.model;

use ClimbSafeStates.ump;
use ClimbSafePersistence.ump;
external interface Serializable {};

class ClimbSafe {


  Date startDate;
  Integer nrWeeks;
  Integer priceOfGuidePerWeek;
  1 <@>- 0..1 Administrator administrator;
  1 <@>- * Guide guides;
  1 <@>- * Member members;
  1 <@>- * BookedItem bookedItems;
  1 <@>- * Equipment equipment;
  1 <@>- * EquipmentBundle bundles;
  1 <@>- * BundleItem bundleItems;
  1 <@>- * Hotel hotels;
  1 <@>- * Assignment assignments;
}

class User {

  abstract;
  unique email;
  password;
}

class Administrator {
  isA User;
 
}

class NamedUser {
  abstract;
  isA User;
  name;
  emergencyContact;
}

class Guide {
  isA NamedUser;
}

class Member {
  isA NamedUser;
  lazy Integer refundPercent;
  Integer nrWeeks;
  Boolean guideRequired;
  Boolean hotelRequired;
}

associationClass BookedItem {

	isA Serializable;
  	depend java.io.Serializable;  
    Integer quantity;
  * Member member;
  * BookableItem item;
   	private static final long serialVersionUID = 53796500899814423L;
   
}

class BookableItem {

  abstract;
  unique name;
}

class Equipment {
  isA BookableItem;
  Integer weight;
  Integer pricePerWeek;
}

class EquipmentBundle {
  isA BookableItem;
  Integer discount;
}

associationClass BundleItem {

	isA Serializable;
  	depend java.io.Serializable;
  Integer quantity;
  * EquipmentBundle bundle;
  * Equipment equipment;
  	private static final long serialVersionUID = 2078963047744599780L;
}

class Hotel {

  unique name;
  address;
  enum HotelRating { OneStar, TwoStars, ThreeStars, FourStars, FiveStars }
  HotelRating rating;
} 

class Assignment {

  Integer startWeek;
  Integer endWeek;
  lazy String authCode;
  0..1 -- 1 Member member;
  * -- 0..1 Guide guide;
  * -- 0..1 Hotel hotel;
}